package reporte.analisis.sintactico;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import reporte.analisis.lexico.LexerReporte;
import reporte.DatosReporte;

parser code
{:

    private ArrayList<String> errores = new ArrayList<>();
    private DatosReporte dtsRep;
    private int contarID = 0;

    public void setDtsRep(DatosReporte dtsRep) {
        this.dtsRep = dtsRep;
    }

    public parser(LexerReporte lexer) {
            super(lexer);
    }

    public void setErrores(ArrayList<String> errores) {
        this.errores = errores;
    }
  
    public void syntax_error(Symbol s) {
        errores.add("Error Sintactico: Lexema: " + String.valueOf(s.value) + " | Linea: " +  s.left + " | columna: " +  s.right);
    }

:};


terminal SCORE, CLASES, VARIABLES, METODOS, COMENTARIOS, NOMBRE, TIPO, FUNCION, PARAMETROS, TEXTO, RESULT, INTEGER,
        STRING, HTML, H1, H2, TABLE, FOR, ITERADOR, HASTA, TR, TD, TH, BR, DECIMAL, SUMA, RESTA,
        MULTIPLICACION, DIVISION, PARENTESIS_A, PARENTESIS_C, PUNTO_COMA, DOS_PUNTOS, COMA, SIGNO_IGUAL, ETIQUETA_ABRIR,
        ETIQUETA_FIN, ETIQUETA_CERRAR, CORCHETE_A, CORCHETE_C, DOLAR, PUNTO, ERROR;

terminal String ID, CADENA;

terminal Integer ENTERO;

non terminal s, sintax_html, variable_h1, body, etiqueta_table, cuerpo_table, fila, fila_th, fila_td,etiqueta_h,
             var, asignacion, var_jison, definicion, var_integer, var_string, identificador,
             columna, etiqueta_for, for_table;

non terminal Integer expresion, expresion_string, valor;

non terminal String c;

precedence left SUMA, RESTA;
precedence left MULTIPLICACION, DIVISION;

start with s;

s ::= 
    var sintax_html 
; 

sintax_html ::=
    ETIQUETA_ABRIR HTML ETIQUETA_CERRAR body ETIQUETA_FIN HTML ETIQUETA_CERRAR
;

variable_h1 ::=
    DOLAR PARENTESIS_A ID PARENTESIS_C DOLAR
    | DOLAR PARENTESIS_A var_jison PARENTESIS_C DOLAR
    | CADENA c
;

body ::=
    body etiqueta_h
    | body etiqueta_table
    | body etiqueta_for
    |
    | error ETIQUETA_CERRAR
;

etiqueta_for ::=
    ETIQUETA_ABRIR FOR ITERADOR DOS_PUNTOS valor HASTA DOS_PUNTOS valor  PUNTO_COMA ETIQUETA_CERRAR
    body
    ETIQUETA_FIN FOR ETIQUETA_CERRAR 
;

etiqueta_table ::=
    ETIQUETA_ABRIR TABLE ETIQUETA_CERRAR 
    cuerpo_table
    ETIQUETA_FIN TABLE ETIQUETA_CERRAR 
;

cuerpo_table ::=
    cuerpo_table etiqueta_h
    | cuerpo_table columna
    | cuerpo_table for_table
    |
;

for_table ::=
    ETIQUETA_ABRIR FOR ITERADOR DOS_PUNTOS valor HASTA DOS_PUNTOS valor  PUNTO_COMA ETIQUETA_CERRAR
    cuerpo_table
    ETIQUETA_FIN FOR ETIQUETA_CERRAR 
;

columna ::=
    ETIQUETA_ABRIR TR ETIQUETA_CERRAR fila  ETIQUETA_FIN TR ETIQUETA_CERRAR 
;

fila ::=
    fila fila_th
    | fila fila_td
    |
;

fila_th ::=
    ETIQUETA_ABRIR TH ETIQUETA_CERRAR variable_h1 ETIQUETA_FIN TH ETIQUETA_CERRAR 
;

fila_td ::=
    ETIQUETA_ABRIR TD ETIQUETA_CERRAR variable_h1 ETIQUETA_FIN TD ETIQUETA_CERRAR 
;


etiqueta_h ::=
    ETIQUETA_ABRIR H1 ETIQUETA_CERRAR variable_h1 ETIQUETA_FIN H1 ETIQUETA_CERRAR
    | ETIQUETA_ABRIR H2 ETIQUETA_CERRAR variable_h1 ETIQUETA_FIN H2 ETIQUETA_CERRAR
;

var ::=
      var definicion
      | var asignacion
      | 
      | error PUNTO_COMA
;

asignacion ::=
    ID:id SIGNO_IGUAL expresion:e PUNTO_COMA {:dtsRep.asignarID(id, String.valueOf(e), idleft);:}
    | ID:id SIGNO_IGUAL CADENA:cad c:resto PUNTO_COMA {:dtsRep.asignarID(id, dtsRep.convertirCadena(cad, resto), idleft);:}
    | ID:id SIGNO_IGUAL ID:asig PUNTO_COMA {:dtsRep.asignarID(id, dtsRep.obtenerID(asig, asigleft), idleft);:}
    | ID:id SIGNO_IGUAL var_jison:var PUNTO_COMA {:dtsRep.asignarID(id, String.valueOf(var), idleft);:}
;

var_jison ::=
    RESULT PUNTO SCORE:val {:RESULT = dtsRep.obtenerValorJISON("Score", 0, null, valleft);:}
    | RESULT PUNTO CLASES
    | RESULT PUNTO CLASES CORCHETE_A valor:val CORCHETE_C {:RESULT = dtsRep.obtenerValorJISON("Clases", val, null, valleft);:}
    | RESULT PUNTO CLASES CORCHETE_A valor:val CORCHETE_C PUNTO NOMBRE {:RESULT = dtsRep.obtenerValorJISON("Clases", val, "Nombre", valleft);:}
    | RESULT PUNTO VARIABLES
    | RESULT PUNTO VARIABLES CORCHETE_A valor:val CORCHETE_C {:RESULT = dtsRep.obtenerValorJISON("Variables", val, null, valleft);:}
    | RESULT PUNTO VARIABLES CORCHETE_A valor:val CORCHETE_C PUNTO NOMBRE {:RESULT = dtsRep.obtenerValorJISON("Variables", val, "Nombre", valleft);:}
    | RESULT PUNTO VARIABLES CORCHETE_A valor:val CORCHETE_C PUNTO TIPO {:RESULT = dtsRep.obtenerValorJISON("Variables", val, "Tipo", valleft);:}
    | RESULT PUNTO VARIABLES CORCHETE_A valor:val CORCHETE_C PUNTO FUNCION {:RESULT = dtsRep.obtenerValorJISON("Variables", val, "Funcion", valleft);:}
    | RESULT PUNTO METODOS
    | RESULT PUNTO METODOS CORCHETE_A valor:val CORCHETE_C {:RESULT = dtsRep.obtenerValorJISON("Metodos", val, null, valleft);:}
    | RESULT PUNTO METODOS CORCHETE_A valor:val CORCHETE_C PUNTO NOMBRE {:RESULT = dtsRep.obtenerValorJISON("Metodos", val, "Nombre", valleft);:}
    | RESULT PUNTO METODOS CORCHETE_A valor:val CORCHETE_C PUNTO TIPO {:RESULT = dtsRep.obtenerValorJISON("Metodos", val, "Tipo", valleft);:}
    | RESULT PUNTO METODOS CORCHETE_A valor:val CORCHETE_C PUNTO PARAMETROS {:RESULT = dtsRep.obtenerValorJISON("Metodos", val, "Parametros", valleft);:}
    | RESULT PUNTO COMENTARIOS
    | RESULT PUNTO COMENTARIOS CORCHETE_A valor:val CORCHETE_C {:RESULT = dtsRep.obtenerValorJISON("Comentarios", val, null, valleft);:}
    | RESULT PUNTO COMENTARIOS CORCHETE_A valor:val CORCHETE_C PUNTO TEXTO {:RESULT = dtsRep.obtenerValorJISON("Comentarios", val, "Texto", valleft);:}
;

valor ::=
    ID:e {:RESULT = Integer.parseInt(dtsRep.obtenerID(e, eleft));:}
    | ENTERO:e {:RESULT = e;:}
;

definicion ::=
    var_integer
    | var_string
;

var_integer ::=
    INTEGER identificador PUNTO_COMA {:dtsRep.agregarSimbolosID("Integer");:}
    | INTEGER ID:id SIGNO_IGUAL expresion:e PUNTO_COMA {:dtsRep.agregarSimboloDeclaracion(id, "Integer", String.valueOf(e), idleft, idright);:}
    | INTEGER ID:id SIGNO_IGUAL var_jison:var PUNTO_COMA {:dtsRep.agregarSimboloDeclaracion(id, "Integer", String.valueOf(var), idleft, idright);:}
;

var_string ::=
    STRING identificador PUNTO_COMA {:dtsRep.agregarSimbolosID("String");:}
    | STRING ID:id SIGNO_IGUAL CADENA:cad c:resto PUNTO_COMA {:dtsRep.agregarSimboloDeclaracion(id, "String",dtsRep.convertirCadena(cad, resto), idleft, idright);:}
    | STRING ID:id SIGNO_IGUAL var_jison:var PUNTO_COMA {:dtsRep.agregarSimboloDeclaracion(id, "String", String.valueOf(var), idleft, idright);:}
;

c ::= 
    c SUMA CADENA:cad {:RESULT = cad.substring(1,cad.length()-1);:}
    | c SUMA expresion_string:e {:RESULT = "" + e;:}
    | c SUMA ID:id {:RESULT = "" + dtsRep.obtenerID(id, idleft);:}
    | c SUMA var_jison:var {:RESULT = String.valueOf(var);:}
    | 
;

identificador ::=
        ID:e {:dtsRep.agregarID(e, eleft, eright);:}
        | identificador COMA ID:e {:dtsRep.agregarID(e,eleft, eright);:}  
;

expresion ::=
           ENTERO:n {: RESULT = n; :}
        | expresion:e1 SUMA:o expresion:e2
                    {: RESULT = e1 + e2; :}
        | expresion:e1 RESTA:o expresion:e2
                    {: RESULT = e1 - e2; :}
        | expresion:e1 MULTIPLICACION:o expresion:e2
                    {: RESULT = e1 * e2; :}
        | expresion:e1 DIVISION:o expresion:e2
                    {: RESULT = e1 / e2;:}
        | RESTA expresion:e 
                    {:RESULT = 0 - e;:}
        | PARENTESIS_A expresion:e PARENTESIS_C
                    {:RESULT = e;:}
;

expresion_string ::=
           ENTERO:n {: RESULT = n; :}
        | expresion_string:e1 SUMA:o expresion_string:e2
                    {: RESULT = e1 + e2; :}
        | expresion_string:e1 RESTA:o expresion_string:e2
                    {: RESULT = e1 - e2; :}
        | expresion_string:e1 MULTIPLICACION:o expresion_string:e2
                    {: RESULT = e1 * e2; :}
        | expresion_string:e1 DIVISION:o expresion_string:e2
                    {: RESULT = e1 / e2;:}
        | RESTA expresion_string:e
                    {:RESULT = 0 - e;:}
        | PARENTESIS_A expresion_string:e PARENTESIS_C
                    {:RESULT = e;:}
;


